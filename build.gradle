plugins {
	id 'java-library'
	id 'idea'
	id 'maven-publish'
	id 'fabric-loom' version '1.2.7'
	id 'com.github.johnrengelman.shadow' version '7.1.0'
}

String bluemapVersion = 'v2.5.1'
String permsVersion = '0.2-SNAPSHOT'

gradle.startParameter.showStacktrace = ShowStacktrace.ALWAYS

version = project.version
logger.lifecycle("Building the project.")

allprojects {
	group = "net.theelm.sewingmachine"
	
	apply plugin: 'java-library'
	apply plugin: 'fabric-loom'
	apply plugin: 'com.github.johnrengelman.shadow'
	
	tasks.withType(JavaCompile) {
		options.release = 17
		options.encoding = "UTF-8"
	}
	
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this task, sources will not be generated.
	task sourcesJar(type: Jar, dependsOn: classes) {
		archiveClassifier.set('sources')
		from sourceSets.main.allSource
	}
	
	java {
		// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
		// if it is present.
		// If you remove this line, sources will not be generated.
		withSourcesJar()
	}
	
	if (project.name == 'sewingmachine-base' || project.name == 'sewingmachine') {
		repositories {
			// Jitpack for Bluemap
			maven { url 'https://jitpack.io' }
		}
	}
	
	repositories {
		// for Fabric Permissions API
		maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
	}
	
	allprojects.each { p ->
		loom.mods.register(p.name) {
			sourceSet p.sourceSets.main
		}
	}
	
	processResources {
		inputs.property 'version', mod_version
		
		afterEvaluate {
			from(sourceSets.main.resources.srcDirs) {
				include 'fabric.mod.json'
				expand 'version': mod_version
				duplicatesStrategy DuplicatesStrategy.INCLUDE
			}
		}
	}
	
	dependencies {
		//to change the versions see the gradle.properties file
		minecraft "com.mojang:minecraft:${project.minecraft_version}"
		mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
		modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
		
		// Fabric API modImplementation (Without including)
		modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
		
		// Permissions API modImplementation (include jar-in-jar)
		include modImplementation(group: 'me.lucko', name: 'fabric-permissions-api', version: permsVersion)
		
		// Include IntelliJ annotations
		implementation 'com.intellij:annotations:+@jar'
		
		// Include necessary dependencies in either BASE or the entire module
		if (project.name == 'sewingmachine' || project.name == 'sewingmachine-base') {
			// Bluemap API
			include modImplementation(group: 'com.github.BlueMap-Minecraft', name: 'BlueMapAPI', version: bluemapVersion)
			
			// Include MySQL
			compileOnly(group: 'mysql', name: 'mysql-connector-java', version: '8.0.15')
		} else {
			// modimplementation will break this
			implementation project(path: ':sewingmachine-base', configuration: 'namedElements')
		}
	}
	
	tasks.withType(AbstractArchiveTask).configureEach {
		preserveFileTimestamps = false
		reproducibleFileOrder = true
	}
}

// Run subproject tasks when running the main task
subprojects.each {
	jar.dependsOn("${it.path}:jar")
	remapJar.dependsOn("${it.path}:remapJar")
	compileJava.dependsOn("${it.path}:compileJava")
}

// All subprojects should depend on the '-base' project
subprojects {
	// Set the name for the subproject jar
	remapJar {
		archiveFileName = "${project.name} ${project.mod_version} mc-${project.minecraft_version}.jar"
	}
}

// Main 'sewing-machine' should depend on all subprojects (package jars)
dependencies {
	subprojects.each {
		implementation project(path: it.path, configuration: 'namedElements')
	}
}

remapJar {
	/*afterEvaluate {
		subprojects.each {
			nestedJars.from project("${it.path}").tasks.getByName("remapJar")
		}
	}*/
	
	//addNestedDependencies = true
	archiveFileName = "${project.name}-${project.mod_version}_mc-${project.minecraft_version}.jar"
}