plugins {
	id 'java-library'
	id 'idea'
	id 'maven-publish'
	id 'fabric-loom' version '1.2.7'
	id 'com.github.johnrengelman.shadow' version '7.1.0'
}

version = project.version
logger.lifecycle("Building the project.")

allprojects {
	group = "net.theelm.sewingmachine"
	
	apply plugin: 'java-library'
	apply plugin: 'fabric-loom'
	apply plugin: 'com.github.johnrengelman.shadow'
	
	tasks.withType(JavaCompile) {
		options.release = 17
		options.encoding = "UTF-8"
	}
	
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this task, sources will not be generated.
	task sourcesJar(type: Jar, dependsOn: classes) {
		archiveClassifier.set('sources')
		from sourceSets.main.allSource
	}
	
	java {
		// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
		// if it is present.
		// If you remove this line, sources will not be generated.
		java.withSourcesJar()
	}
	
	allprojects.each { p ->
		loom.mods.register(p.name) {
			sourceSet p.sourceSets.main
		}
	}
	
	processResources {
		inputs.property 'version', version
		
		afterEvaluate {
			from(sourceSets.main.resources.srcDirs) {
				include 'fabric.mod.json'
				expand 'version': version
				duplicatesStrategy DuplicatesStrategy.INCLUDE
			}
		}
	}
	
	dependencies {
		//to change the versions see the gradle.properties file
		minecraft "com.mojang:minecraft:${project.minecraft_version}"
		mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
		modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
		
		// Fabric API. Used for commands
		modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
		
		implementation 'com.intellij:annotations:+@jar'
		
		if (project.name != "sewingmachine-base") {
			implementation project(path: ':sewingmachine-base', configuration: "namedElements")
		}
	}
	
	tasks.withType(AbstractArchiveTask).configureEach {
		preserveFileTimestamps = false
		reproducibleFileOrder = true
	}
	
	/*tasks.register('generateSources') {
		group = "fabric"
	}*/
	
	/*remapJar {
		archiveFileName = "${project.name}-${project.mod_version}_mc-${project.minecraft_version}.jar"
	}*/
}